*** Settings ***

Resource          variables.txt

Library           AndroidLibrary
Library           OperatingSystem

#Suite Setup       Setup Suite
#Suite Teardown    Stop Emulator

Documentation     Tests the ApiDemos.apk that is included in the Android SDK

*** Keywords ***

Setup Suite

    Android SDK Should Exist
    Update Android SDK
    Create Android Virtual Device

    ${HEADLESS_BOOL}=             Convert To Boolean        ${HEADLESS}
    Start Emulator                ${EMULATOR_NAME}          no_window=${HEADLESS_BOOL}

    Pull ApiDemos.apk from Device
    Re-Sign ApiDemos.apk with Debug Keystore
    Build Instrumentation App

    Install App

    Stop Emulator

Android SDK Should Exist
    [Documentation]               simple sanity check to see if %{ANDROID_HOME} was set correctly
    File Should Exist             %{ANDROID_HOME}/tools/android

Update Android SDK
    Execute                       %{ANDROID_HOME}/tools/android update sdk -t android-${API_LEVEL} --no-ui

Create Android Virtual Device
    Execute                       echo "no" | %{ANDROID_HOME}/tools/android --silent create avd --name ${EMULATOR_NAME} --force -t android-${API_LEVEL}

Pull ApiDemos.apk from Device
    [Timeout]                     2 minutes

    Wait Until Keyword Succeeds   5 seconds  30 seconds
    ...                           Wait For Device

    Remove File                   ApiDemos.apk

    Wait Until Keyword Succeeds   5 seconds  30 seconds
    ...  Execute                  %{ANDROID_HOME}/platform-tools/adb pull /data/app/ApiDemos.apk

Re-Sign ApiDemos.apk with Debug Keystore
    File Should Exist             ApiDemos.apk
    Execute                       zip -d ApiDemos.apk META-INF/*
    Execute                       echo "android" | jarsigner -verbose -keystore $HOME/.android/debug.keystore ApiDemos.apk androiddebugkey

Build Instrumentation App
    Remove Directory              ${EXECDIR}/test_servers/                 recursive=True
    Execute                       calabash-android build ApiDemos.apk

    ${TEST_SERVERS}=              List Directory    ${EXECDIR}/test_servers/
    Set Global Variable           ${TEST_APK}       ${TEST_SERVERS[0]}

Install App
    [Timeout]                     2 minutes

    Wait for Device

    Wait Until Keyword Succeeds   5 seconds  30 seconds
    ...  Uninstall Application    com.example.android.apis

    Wait Until Keyword Succeeds   5 seconds  30 seconds
    ...  Uninstall Application    com.example.android.apis.test

    Wait Until Keyword Succeeds   5 seconds  30 seconds
    ...  Install Application      ${EXECDIR}/test_servers/${TEST_APK}

    Wait Until Keyword Succeeds   5 seconds  30 seconds
    ...  Install Application      ${EXECDIR}/ApiDemos.apk

